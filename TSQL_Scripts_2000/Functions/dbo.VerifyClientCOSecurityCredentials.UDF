SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS ON 
GO

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[VerifyClientCOSecurityCredentials]') and xtype in (N'FN', N'IF', N'TF'))
drop function [dbo].[VerifyClientCOSecurityCredentials]
GO

/******************************************************************
*
*    	Description:	Check Security Credentials against User ID for Assignment ID
*	
*	Author: 	Brad Skidmore
*	Date: 		5/9/2005
*
******************************************************************/

CREATE FUNCTION dbo.VerifyClientCOSecurityCredentials
(
	@UsersID int,
	@CompanyCode varchar(3)
)
RETURNS bit
AS
BEGIN
	DECLARE @CredentialVerified As Bit
	DECLARE @FindClientCompanyID As int
	DECLARE @FindUsersID As int
	DECLARE @Active As Bit

	SELECT 	@FindClientCompanyID = [CompanyID],
		@Active = [Active]
	FROM	Company
	WHERE	Code = @CompanyCode
	
	--If the Code is not found... It could be becuase the Code
	-- for the Company changed.
	-- Loo under history to find the Company
	IF @FindClientCompanyID Is Null
	BEGIN
		SELECT 	Top 1 @FindClientCompanyID = [CompanyID]
		FROM	CompanyHistory
		WHERE	Code = @CompanyCode
		ORDER BY [DateLastUpdated] DESC
		
		-- If the Company ID was found under an old Code
		-- Need to verify that this same company is Active under the 
		-- Company Table.
		IF @FindClientCompanyID Is Not Null
		BEGIN
			SELECT	@Active = [Active]
			FROM	Company
			WHERE	[CompanyID] = @FindClientCompanyID
		END
	END
	--Only Look for USer Assigned to this Company if Company is Found
	IF @FindClientCompanyID Is Not Null 
	BEGIN
		-- Only Look for User if Found Company Is Still Active
		IF @Active = 1 
		BEGIN
			SELECT 	TOP 1 @FindUsersID = [UsersID]
			FROM	CompanyUsers
			WHERE	[CompanyID] = @FindClientCompanyID
			AND	[UsersID] = @UsersID
			AND 	[Active] = 1
		END
	END
	

	IF @FindUsersID = @UsersID
	BEGIN
		SET @CredentialVerified = 1
	END 
	ELSE
	BEGIN
		SET @CredentialVerified = 0
	END

	RETURN(@CredentialVerified)
END

GO
SET QUOTED_IDENTIFIER OFF 
GO
SET ANSI_NULLS ON 
GO

GRANT  EXECUTE  ON [dbo].[VerifyClientCOSecurityCredentials]  TO [CFUSER]
GO

